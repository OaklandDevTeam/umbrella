plugins {
    id 'java'
    id 'idea'
    id "com.github.node-gradle.node" version "2.2.4"
}

group 'com.umbr3ll4'
version '1.0-SNAPSHOT'

jar {
    manifest {
        attributes(
                'Main-Class': 'com.umbr3114.Main'
        )
    }
    from {
        configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) }
    }
}

processResources {
    dependsOn(":frontend:buildWebpack")
    from(file("${project.projectDir}/frontend/build/frontend/"))
    from(file("${project.projectDir}/dbconnection.properties"))
}

repositories {
    mavenCentral()
}

dependencies {
    
    // main REST framework
    implementation "com.sparkjava:spark-core:2.9.1"

    // This also imports Jackson, Jackson-databind, Jackson-annotations
    // and the mongo driver
    implementation group: 'org.mongojack', name: 'mongojack', version: '4.0.0'

    // https://mvnrepository.com/artifact/org.mindrot/jbcrypt
    implementation group: 'org.mindrot', name: 'jbcrypt', version: '0.4'

    // logging
    implementation group: 'org.slf4j', name: 'slf4j-simple', version: '1.7.25'

    testImplementation group: 'junit', name: 'junit', version: '4.12'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '3.3.3'

}

task copyDbProperties(type: Copy) {
    from(file("${project.projectDir}/dbconnection.properties"))
    into(file("${project.projectDir}/src/main/resources"))
}

task cleanDbProperties(type: Delete) {
    delete "${project.projectDir}/src/main/resources/dbconnection.properties"
}

//processResources.dependsOn copyDbProperties

clean.dependsOn cleanDbProperties